// This Pine Scriptâ„¢ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© marekhanzal

//@version=5
// strategy("PiPi", overlay = true, default_qty_value = 2, currency = currency.USD, pyramiding = 1, process_orders_on_close = false, initial_capital = 1000, max_lines_count = 500, max_bars_back = 4900, max_boxes_count = 500, max_labels_count = 500, calc_on_order_fills = false)
indicator("PiPi", overlay = true, max_lines_count = 500, max_bars_back = 4900, max_boxes_count = 500, max_labels_count = 500)

//  ====================================================================================================================================================
//      Library: Level
//  ====================================================================================================================================================

	//  ====================================================================================================================================================
	//      Types
	//  ====================================================================================================================================================

//	@type			This is a level; because trading is not about exact numbers, Level is represented by a range (low and high); for a single line both can be the same value
//	@field	name	Level name used to programatically identify it's kind
//	@field	low		Level low value; for single value levels, both should be same
//	@field	high	Level high value; for single value levels, both should be same
//	@field	level	Level middle value
//	@field	size	Absolute diff between low and high
type Level
	string	name
	float	low
	float	high
	float	level
	float	size

	//	---		Factory methods

//	@function			Default level factory with low/high; level (middle) and size (high - low) is computed automatically
//	@param		name	Level name
//	@param		low		Low value (level size and middle is based on both low and high)
//	@param		high	High value (level size and middle is based on both low and high)
level_create(string name, float low, float high)		=>	Level.new(name, low, high, (high - low) / 2, math.abs(high - low))

//	@function	Creates "na" level
level_create()											=>  Level.new(na, na, na, na, na)

//	@function			Creates	level based on value and low/high based on the factor
//	@param		name	Level name
//	@param		level	Value
//	@param		size	Level size: low = level - size; high = level + size
level_size(string name, float level, float size)	=>	Level.new(name, level - size, level + size, level, size * 2)

	//  ====================================================================================================================================================
	//      Mutations
	//  ====================================================================================================================================================

//	@function	Sets new level low; also updates "level" and "size"
level_low(Level level, float low) =>
	if not na(level)
		level.low	:=	low
		level.level	:=	(level.high + level.low) / 2
		level.size	:=	math.abs(level.low - level.high)

//	@function	Sets new level high; also updates "level" and "size"
level_high(Level level, float high) =>
	if not na(level)
		level.high	:=	high
		level.level	:=	(level.high + level.low) / 2
		level.size	:=	math.abs(level.low - level.high)

//	@function	Set both low and high and updates "level" and "size"
level_low_high(Level level, float low, float high) =>
	level_low(level, low)
	level_high(level, high)

//	@function	Push new level into levels map using "level" and "size"
level_push_size(map<string, Level> levels, string name, float level, float size) =>
    levels.put(name, level_size(name, level, size))

	//  ====================================================================================================================================================
	//      Tools
	//  ====================================================================================================================================================

//	@function	Checks if the given value is inside a level
level_inside(Level level, float value) =>	value >= level.low and value <= level.high

	//  ====================================================================================================================================================
	//      Rendering
	//  ====================================================================================================================================================

//	@function			Simple function to render a level
//	@param		name	Explicit level name used to track rendering of primitives
//	@param		level	So, you want render a Level?
//	@param		index	Start index (bar_index)
//	@param		last	Last index; "na" for extend right
//	@param		color	Yes, color
//	@param		zone	Simple mode draws only single line (Levels "level"), otherwise all level lines are rendered (low, high, level)
level_render_zone(string name, Level level, int index, int last, color color, bool zone = false) =>
	var line	_line_low		=	na
	var line	_line_level		=	na
	var line	_line_high		=	na
	var box		_box			=	na

	line.delete(_line_low)
	line.delete(_line_level)
	line.delete(_line_high)

	box.delete(_box)

	if not na(level)
		_index	=	na(index)	? bar_index		: index
		_last	=	na(last)	? index + 1		: last
		_extend	=	na(last)	? extend.right	: extend.none
		_extend:=	na(last) and na(index) ? extend.both : _extend

		_line_level:=	line.new(_index,	level.level,	_last,	level.level,	style = line.style_solid,	extend = _extend, color = color, width = 1)

		if zone
			_line_low	:=	line.new(_index,	level.low,	_last,	level.low, 		style = line.style_dotted,	extend = _extend, color = color, width = 1)
			_line_high	:=	line.new(_index,	level.high,	_last,	level.high,		style = line.style_dotted,	extend = _extend, color = color, width = 1)
			_box		:=	box.new(_index,		level.high,	_last,	level.low,									extend = _extend, border_color = na, border_width = na, bgcolor = color.new(color, 80))

	true

//  ====================================================================================================================================================
//      Library: Toolbox
//  ====================================================================================================================================================

	//  ====================================================================================================================================================
	//      Utilities
	//  ====================================================================================================================================================

	//  --- Hack used to call block one time only, useful for setting up var arrays/maps which cannot be created in one statement

use_init() =>
	var bool	init	= true
	bool		_init	= init
	init:=	false

	_init

	//  ---		Somehow independent Fibbonachi level computation

fibbonachi(Level level, float factor = 0) =>
	_diff 		= math.abs(level.high - level.low)
	_base		= level.high
	_fibb		= map.new<string, Level>()

	level_push_size(_fibb, "0", 		_base - (_diff * 0.000), factor)
	level_push_size(_fibb, "20",		_base - (_diff * 0.236), factor)
	level_push_size(_fibb, "30",		_base - (_diff * 0.382), factor)
	level_push_size(_fibb, "50",		_base - (_diff * 0.500), factor)
	level_push_size(_fibb, "60",		_base - (_diff * 0.618), factor)
	level_push_size(_fibb, "70",		_base - (_diff * 0.786), factor)
	level_push_size(_fibb, "100",		_base - (_diff * 1.000), factor)

	_fibb

//  ====================================================================================================================================================
//      Library: Context
//  ====================================================================================================================================================

	//  ====================================================================================================================================================
	//      Types
	//  ====================================================================================================================================================

//	@type			Value represents PineScript's type accessible from PiPi Script; usually only one value should be set
//	@field	name	Represents any string value
//	@field	flag	Represents true/false state, for example uptrend, crossings or whatever
//	@field	value	Could be any static or computed value, for example day's price range
//	@field	level	Represents a level; could be a single number or (usually) a range
type Value
	string		_string
	bool	    _bool
	float		_float
	Level		_level

//	@type			Most important piece of this indicator - there are providers of a context, each managing it's internal state; Context is also used as a communication interface between providers and PiPi Script
//	@field	name	Name of a context, referenced from PiPi Script and providers
//	@field	value	Holds all the variables accessible from the PiPi script
type Context
	string					name
	map<string, Value>		value

//	@type			Just simple wrapper, so we can add contexts to an array
type Contexts
	map<string, Context>	map

	//  ---     Value Factory methods

value_create(string _string)								=>	Value.new(_string, na, na, na)
value_create(bool _bool)									=>	Value.new(na, _bool, na, na)
value_create(float _float)									=>	Value.new(na, na, _float, na)
value_create(Level _level)									=>	Value.new(na, na, na, _level)

	//			Value utility methods

//	@function	If "value" is "na", return "default"; just shorthand for a bit better readability
value_na(Value value, Value default)						=>	na(value) ? default : value
value_copy(Value value)										=>	na(value._level) ? value.copy() : value_create(value._level.copy())

	//  ---     Context Factory methods

context_create(string name)									=>  Context.new(name, map.new<string, Value>())
contexts_create()											=>  Contexts.new(map.new<string, Context>())

	//  ====================================================================================================================================================
	//      Context methods
	//  ====================================================================================================================================================

	//  ---     Context - Utilities

use_context(Contexts contexts, Context context)	=>
	contexts.map.put(context.name, context)
	context

context(Context context, Context default)	=>	na(context) ? default : context

contexts_context(Contexts contexts, string context) =>
	_context = contexts.map.contains(context) ? contexts.map.get(context) : context_create(context)
	contexts.map.put(context, _context)
	_context

	//  ---     Context - Setters

context_set(Context context, string name, Value value)		    =>
	context.value.put(name, value)
	value

context_set(Context context, string name, string value)		    =>
	context_set(context, name, value_create(value))
	value

context_set(Context context, string name, bool value)		    =>
	context_set(context, name, value_create(value))
	value

context_set(Context context, string name, float value)		    =>
	context_set(context, name, value_create(value))
	value

context_set(Context context, string name, Level value)			=>
	context_set(context, name, value_create(value))

	context_set(context, name + ".low",		value.low)
	context_set(context, name + ".high",	value.high)
	context_set(context, name + ".level",	value.level)
	context_set(context, name + ".size",	value.size)

	value

context_set(Context context, Level value)				    	=>
	context_set(context, value.name, value)

context_set(Context context, map<string, Level> levels)    		=>
	for [__name, __level] in levels
		context_set(context, __name, __level)

	levels

	//  ---     Contexts - Setters

contexts_set(Contexts contexts, string context, string name, string			value)	=>	context_set(contexts_context(contexts, context), name, value)
contexts_set(Contexts contexts, string context, string name, float			value)	=>	context_set(contexts_context(contexts, context), name, value)
contexts_set(Contexts contexts, string context, string name, bool			value)	=>	context_set(contexts_context(contexts, context), name, value)
contexts_set(Contexts contexts, string context, string name, Level			value)	=>	context_set(contexts_context(contexts, context), name, value)

	//  ---     Context - Getters

	//  Common getters

context_get			(Context context, string name, Value	default)		=>
	if not context.value.contains(name)
		log.warning("unknown value [{0}.{1}]", context.name, name)

	value_na(context.value.get(name), default)

context_get_string  (Context context, string name, string 		default	= na)		=>	context_get(	context, name,		value_create(default))._string
context_get_bool    (Context context, string name, bool			default	= na)		=>	context_get(	context, name,		value_create(default))._bool
context_get_float	(Context context, string name, float		default	= na)		=>	context_get(	context, name,		value_create(default))._float
context_get_int		(Context context, string name, int			default	= na)		=>	int(context_get(context, name,		value_create(default))._float)
context_get_level	(Context context, string name, Level		default	= na)	    =>	context_get(	context, name,		value_create(default))._level

	//  Contexts getters

contexts_get_string	(Contexts contexts, string context, string name, string			default	= na)	=>	context_get_string( context(contexts.map.get(context),	context_create(context)), name, default)
contexts_get_bool	(Contexts contexts, string context, string name, bool			default	= na)	=>	context_get_bool(   context(contexts.map.get(context),	context_create(context)), name, default)
contexts_get_float	(Contexts contexts, string context, string name, float			default	= na)	=>	context_get_float(  context(contexts.map.get(context),	context_create(context)), name, default)
contexts_get_int	(Contexts contexts, string context, string name, int 			default	= na)	=>	context_get_int(    context(contexts.map.get(context),	context_create(context)), name, default)
contexts_get_level	(Contexts contexts, string context, string name, Level			default	= na)	=>	context_get_level(  context(contexts.map.get(context),	context_create(context)), name, default)

	//  Context access getters

contexts_get_string(Contexts contexts, string[] value) =>
	switch value.size()
		1 =>	value.get(0)
		2 =>	contexts_get_string(contexts, value.get(0), value.get(1))

contexts_get_bool(Contexts contexts, string[] value) =>
	switch value.size()
		1 =>
			switch value.get(0)
				"true"	=> true
				"false"	=> false
				=> na

		2 => contexts_get_bool(contexts, value.get(0), value.get(1))

contexts_get_float(Contexts contexts, string[] value) =>
	switch value.size()
		1 =>	str.tonumber(value.get(0))
		2 =>	contexts_get_float(contexts, value.get(0), value.get(1))
		3 =>	contexts_get_float(contexts, value.get(0), value.get(1)) * str.tonumber(value.get(2))

//  ====================================================================================================================================================
//      Library: source
//			Here are all sources (contexts providers) for PiPi
//  ====================================================================================================================================================

	//  ====================================================================================================================================================
	//      Day-Low-High provider
	//  ====================================================================================================================================================

//	@function	Manages context of current Day Low/High and related events
use_day(Contexts contexts) =>
	var Context		_context	=	context_create("day")
	var	Level		_level		=	level_create("day", low, high)

	context_set(_context, "change",			false)
	context_set(_context, "new-day",		false)
	context_set(_context, "higher-high",	false)
	context_set(_context, "lower-low",		false)
	context_set(_context, "level",			_level)

	if bool(ta.change(time('D')))
		level_low_high(_level, low, high)

		context_set(_context, "change",			true)
		context_set(_context, "new-day",		true)
		context_set(_context, "level",			_level)

	else if low < _level.low
		level_low(_level, low)

		context_set(_context, "change",			true)
		context_set(_context, "lower-low",		true)
		context_set(_context, "level", 			_level)

	else if high > _level.high
		level_high(_level, high)

		context_set(_context, "change",			true)
		context_set(_context, "higher-high",	true)
		context_set(_context, "level",			_level)

	use_context(contexts, _context)

	//  ====================================================================================================================================================
	//      Day-Low-High Fibbonachi provider
	//  ====================================================================================================================================================

//	@function	Listens for changes of Day Low/High and adjusts Fibbonachi levels accordingly
use_day_fibbonachi(Contexts contexts) =>
	var Context	_context	=	context_create("day-fibbonachi")
	Level		_level		=	contexts_get_level(contexts, "day", "level")

	//	We don't want to execute Fibb computation on every bar - don't waste TradingView's resources on the same computations
	if use_init()
		context_set(_context, fibbonachi(_level, 0))

	//	Same here - we're computing day's fibbonachi, so listen for changes comming from "use_day_low_high" or who provides the vallues
	if contexts_get_bool(contexts, "day", "change")
		context_set(_context, fibbonachi(_level, 0))

	use_context(contexts, _context)

	//  ====================================================================================================================================================
	//      EMA
	//  ====================================================================================================================================================

use_ema(Contexts contexts, int length_1, int length_2, int length_3, int length_4, int length_5, int length_6) =>
	var Context		_context	=	context_create("ema")

	_ema_1	=	ta.ema(close, length_1)
	_ema_2	=	ta.ema(close, length_2)
	_ema_3	=	ta.ema(close, length_3)
	_ema_4	=	ta.ema(close, length_4)
	_ema_5	=	ta.ema(close, length_5)
	_ema_6	=	ta.ema(close, length_6)

	context_set(_context,	level_size("1", _ema_1, 0))
	context_set(_context,	level_size("2", _ema_2, 0))
	context_set(_context,	level_size("3", _ema_3, 0))
	context_set(_context,	level_size("4", _ema_4, 0))
	context_set(_context,	level_size("5", _ema_5, 0))
	context_set(_context,	level_size("6", _ema_6, 0))

	_6_1	=	_ema_1 < _ema_2 and _ema_2 < _ema_3 and _ema_3 < _ema_4 and _ema_5 < _ema_6
	_1_6	=	_ema_1 > _ema_2 and _ema_2 > _ema_3 and _ema_3 > _ema_4 and _ema_5 > _ema_6
	context_set(_context,	"6-1",					_6_1)
	context_set(_context,	"6-1.signal.enter",		_6_1 and not _6_1[1])
	context_set(_context,	"6-1.signal.exit",	not _6_1 and _6_1[1])

	context_set(_context,	"1-6",					_1_6)
	context_set(_context,	"1-6.signal.enter",		_1_6 and not _1_6[1])
	context_set(_context,	"1-6.signal.exit",	not	_1_6 and _1_6[1])

	use_context(contexts, _context)

	//  ====================================================================================================================================================
	//      CCI
	//  ====================================================================================================================================================

use_cci(Contexts contexts, int length) =>
	var Context	_context	=	context_create("cci")

	_signal = ta.cci(hlc3, length)

	context_set(_context, "below",				_signal 	< 	-100)
	context_set(_context, "below.exit",			_signal[1]	<	-100	and _signal 	> -100)
	context_set(_context, "below.enter",		_signal		<	-100	and _signal[1]	> -100)

	context_set(_context, "above",				_signal 	> 	100)
	context_set(_context, "above.enter", 		_signal[1]	<	100		and _signal		> 100)
	context_set(_context, "above.exit", 		_signal[1]	>	100		and _signal		< 100)

	context_set(_context, "lower",				_signal 	< 	0)
	context_set(_context, "higher",				_signal 	> 	0)

	context_set(_context, "lower.signal",		_signal 	< 	0 and _signal[1] >= 0)
	context_set(_context, "higher.signal",		_signal 	> 	0 and _signal[1] <= 0)

	context_set(_context, "zone", 				_signal 	>= -100		and _signal		<= 100)

	context_set(_context, "signal", 			_signal)

	use_context(contexts, _context)

	//  ====================================================================================================================================================
	//      Volume analytics
	//  ====================================================================================================================================================

use_volume(Contexts contexts) =>
    var Context	_context	=	context_create("volume")
    var             _low        =   volume
    var             _high       =   volume

    context_set(_context, "min",    false)
    context_set(_context, "max",    false)

    context_set(_context, "lower",  volume < volume[1])
    context_set(_context, "higher", volume > volume[1])

    context_set(_context, "size",   volume)

    if volume < _low
        context_set(_context, "min", true)
        _low:=  volume

    else if volume > _high
        context_set(_context, "max", true)
        _high:= volume

    if bool(ta.change(time('D')))
        _low:=  volume
        _high:= volume

    context_set(_context, "range",      _high - _low)
    context_set(_context, "percent",   (100 * volume) / ( _high - _low))

    use_context(contexts, _context)

	//  ====================================================================================================================================================
	//      Simple Structure and POI detection
	//			This indicator tries to find interesting points, where a lot of candles "meet", potentially showing lows/highs/supports/resistances based
	//			on candles in the given line/zone.
	//  ====================================================================================================================================================

use_poi(Contexts contexts, int short_length, float short_size, int short_count, int short_offset, color short_color, int long_length, float long_size, int long_count, int long_offset, color long_color) =>
	var Context		_context			=	context_create("poi")
	Level			_level_short_low	=	level_size("short-low",		math.min(open, close), short_size)
	Level			_level_short_high	=	level_size("short-high",	math.max(open, close), short_size)
	Level			_level_long_low		=	level_size("long-low",		math.min(open, close), long_size)
	Level			_level_long_high	=	level_size("long-high",		math.max(open, close), long_size)
	int				_count				=	0

	_count:=	0
	if short_length > 0
		for int __index = short_offset to short_length
			Level 	__match	=	na

			if level_inside(_level_short_low, open[__index]) or level_inside(_level_short_low, close[__index])
				_count	+=	1
				__match:=	_level_short_low
			else if level_inside(_level_short_high, open[__index]) or level_inside(_level_short_high, close[__index])
				_count	+=	1
				__match:=	_level_short_high

			if not na(__match) and _count >= short_count
				level_render_zone("poi-short", __match, bar_index - __index, bar_index, short_color, true)
				break

	_count:=	0
	if long_length > 0
		for int __index = long_offset to long_length
			Level 	__match	=	na

			if level_inside(_level_long_low, open[__index]) or level_inside(_level_long_low, close[__index])
				_count	+=	1
				__match:=	_level_long_low
			else if level_inside(_level_long_high, open[__index]) or level_inside(_level_long_high, close[__index])
				_count	+=	1
				__match:=	_level_long_high

			if not na(__match) and _count >= long_count
				level_render_zone("poi-long", __match, bar_index - __index, bar_index, long_color, true)
				break

	use_context(contexts, _context)

use_rotation(Contexts contexts, int length, float size, int count, color color) =>
	var Context	_context	=	context_create("rotation")
	Level		_level		=	level_create("rotation", low - size, high + size)
	Level 		_match	=	na

	_count	=	0
	for int __index	= 0 to length
		if level_inside(_level, open[__index]) and level_inside(_level, close[__index])
			_count	+=	1
			_match	:=	_level
		else
			break

	if not na(_match) and _count >= count
		level_render_zone("rotation", _match, bar_index - (_count - 1), bar_index, color, true)

	use_context(contexts, _context)


//  ====================================================================================================================================================
//      PiPi Local code
//			All the stuff here is "proprietary" for PiPi, thus it's not "possible" to move those things into a lib.
//  ====================================================================================================================================================

	//  ====================================================================================================================================================
	//      Inputs
	//			All the inputs needed by this indicator.
	//  ====================================================================================================================================================

const string	group_poi_short		=	"- Point-of-Interest: Short -"
var int			i_poi_short_length	=	input.int(		30,		"Length",			minval = 0, 	group = group_poi_short, display = display.none)
var float		i_poi_short_size	=	input.float(	0.1,	"Size",				minval = 0.05, step = 0.05, group = group_poi_short, display = display.none)
var int			i_poi_short_count	=	input.int(		3,		"Count",			minval = 1, 	group = group_poi_short, display = display.none)
var int			i_poi_short_offset	=	input.int(		2,		"Offset",			minval = 2, 	group = group_poi_short, display = display.none)
var color		i_poi_short_color	=	input.color(	color.fuchsia,	"Color",	group = group_poi_short, display = display.none)

const string	group_poi_long		=	"- Point-of-Interest: Long -"
var int			i_poi_long_length	=	input.int(		1000,	"Length",	minval = 0, 				group = group_poi_long, display = display.none, tooltip = "Set long distance detection:\nLength - number of bars of lookback\nSize - Detection tolerance\nOffset - How many bars to skip before starting match\nColor. Yes, color. You know that")
var float		i_poi_long_size		=	input.float(	0.2,	"Size",		minval = 0.05, step = 0.05, group = group_poi_long, display = display.none)
var int			i_poi_long_count	=	input.int(		20,		"Count",	minval = 1, 				group = group_poi_long, display = display.none)
var int			i_poi_long_offset	=	input.int(		0,		"Offset",	minval = 0, 				group = group_poi_long, display = display.none)
var color		i_poi_long_color	=	input.color(	color.maroon,	"Color",					group = group_poi_long, display = display.none)

const string	group_rotation		=	"- Rotation -"
var int			i_rotation_length	=	input.int(		15,		"Length",		minval = 0, 					group = group_rotation, display = display.none)
var float		i_rotation_size		=	input.float(	1.25,	"Size",			minval = 0.05, step = 0.05,		group = group_rotation, display = display.none)
var int			i_rotation_count	=	input.int(		6,		"Count",		minval = 1, 					group = group_rotation, display = display.none)
var color		i_rotation_color	=	input.color(	color.gray,	"Color",					group = group_rotation, display = display.none)

const string	group_cci			=	"- CCI -"
var int			i_cci_length		=	input.int(	36,		"â†” Length",					inline = "cci", group = group_cci, display = display.none)
var bool		i_cci_markers		=	input.bool(	true,	"Markers",					inline = "cci", group = group_cci, display = display.none)

const string	group_ema			=	"- EMA -"
var int			i_ema_length_1		=	input.int(		4,				"EMA #1-2",		group = group_ema, display = display.none,	inline = "ema1")
var int			i_ema_length_2		=	input.int(		8,				"",				group = group_ema, display = display.none,	inline = "ema1")
var int			i_ema_length_3		=	input.int(		12,				"EMA #3-4",		group = group_ema, display = display.none,	inline = "ema2")
var int			i_ema_length_4		=	input.int(		16,				"",				group = group_ema, display = display.none,	inline = "ema2")
var int			i_ema_length_5		=	input.int(		20,				"EMA #5-6",		group = group_ema, display = display.none,	inline = "ema3")
var int			i_ema_length_6		=	input.int(		24,				"",				group = group_ema, display = display.none,	inline = "ema3")

var bool		i_ema_markers		=	input.bool(		false,		"Markers",		inline = "ema", group = group_ema, display = display.none, tooltip = "Mark ordered EMAs:\nWhen green, signals are 1 > 6\nWhen red, signals are 1 < 6")
var color		i_ema_green			=	input.color(	#00aa33,	"",				inline = "ema", group = group_ema, display = display.none)
var color		i_ema_red			=	input.color(	#a00d3e,	"",				inline = "ema", group = group_ema, display = display.none)

_fibb_alpha	=	0
const string	group_day					=	"- Day Structure -"
var bool		i_day_level					=	input.bool(		false,			"ðŸ“… Day Low/High",		group = group_day, display = display.none,	inline = "day")
var color		i_day_high_color			=	input.color(	color.new(color.teal, 50),	"",			group = group_day, display = display.none,	inline = "day")
var color		i_day_low_color				=	input.color(	color.new(color.fuchsia, 50),	"",		group = group_day, display = display.none,	inline = "day")
var bool		i_day_fibb					=	input.bool(		true,			"Î¦ Fibbonachi",			group = group_day, display = display.none,	inline = "day-fibb",			tooltip = "Display day's low/high + it's fibbonachi levels; factor is fibbonachi level size")
var color		i_day_fibb_color_1			=	input.color(	color.new(color.red,		_fibb_alpha),	"",			group = group_day, display = display.none,	inline = "day-fibb-color")
var color		i_day_fibb_color_2			=	input.color(	color.new(color.yellow,	_fibb_alpha),	"",			group = group_day, display = display.none,	inline = "day-fibb-color")
var color		i_day_fibb_color_3			=	input.color(	color.new(color.orange,	_fibb_alpha),	"",			group = group_day, display = display.none,	inline = "day-fibb-color")
var color		i_day_fibb_color_4			=	input.color(	color.new(color.green,		_fibb_alpha),	"",			group = group_day, display = display.none,	inline = "day-fibb-color")
var color		i_day_fibb_color_5			=	input.color(	color.new(color.teal,		_fibb_alpha),	"",			group = group_day, display = display.none,	inline = "day-fibb-color")
var color		i_day_fibb_color_6			=	input.color(	color.new(color.aqua,		_fibb_alpha),	"",			group = group_day, display = display.none,	inline = "day-fibb-color")
var color		i_day_fibb_color_7			=	input.color(	color.new(color.blue,		_fibb_alpha),	"",			group = group_day, display = display.none,	inline = "day-fibb-color")

	//  ====================================================================================================================================================
	//      Main Entry
	//			This is main entry point of this system; all the indicators, workflow, script parsing and so on are here.
	//
	//		I don't like this way, but this method is directly connected to inputs of this indicator as it's not possible in a nice way to pass them down
	//		properly.
	//  ====================================================================================================================================================

//	@function	Main entry point of this indicator; even without any plotting, it could be used for strategy testing using TradingView built-in tester
main() =>
    Contexts	_contexts   =   contexts_create()

    use_day              		(_contexts)
    use_day_fibbonachi   		(_contexts)
    use_ema              		(_contexts, i_ema_length_1, i_ema_length_2, i_ema_length_3, i_ema_length_4, i_ema_length_5, i_ema_length_6)
    use_cci              		(_contexts, i_cci_length)
	use_volume					(_contexts)
	use_poi						(_contexts, i_poi_short_length, i_poi_short_size, i_poi_short_count, i_poi_short_offset, i_poi_short_color, i_poi_long_length, i_poi_long_size, i_poi_long_count, i_poi_long_offset, i_poi_long_color)
	use_rotation				(_contexts, i_rotation_length, i_rotation_size, i_rotation_count, i_rotation_color)

	_contexts

	//  ====================================================================================================================================================
	//      Main Entry execution
	//  ====================================================================================================================================================

_contexts	=	main()

_shape_size		=	size.small
_label_alpha	=	75

	//  ====================================================================================================================================================
	//      Render day's low and high
	//  ====================================================================================================================================================
_plot_high	=	plot(contexts_get_level(_contexts, "day", "level").high,	style =	plot.style_steplinebr,	linewidth = 2,	display = i_day_level ? display.pane : display.none,	color = i_day_high_color,	title = "Day - Upper Level")
_plot_low	=	plot(contexts_get_level(_contexts, "day", "level").low,		style =	plot.style_steplinebr,	linewidth = 2,	display = i_day_level ? display.pane : display.none,	color = i_day_low_color,	title = "Day - Lower Level")

	//  ====================================================================================================================================================
	//      Render Fibbonachi levels
	//  ====================================================================================================================================================

if i_day_fibb
	level_render_zone("day-fibb-0",		contexts_get_level(_contexts,	"day-fibbonachi", "0"),		na, na, i_day_fibb_color_1, false)
	level_render_zone("day-fibb-20",	contexts_get_level(_contexts,	"day-fibbonachi", "20"),	na, na, i_day_fibb_color_2, false)
	level_render_zone("day-fibb-30",	contexts_get_level(_contexts,	"day-fibbonachi", "30"),	na, na, i_day_fibb_color_3, false)
	level_render_zone("day-fibb-50",	contexts_get_level(_contexts,	"day-fibbonachi", "50"),	na, na, i_day_fibb_color_4, false)
	level_render_zone("day-fibb-60",	contexts_get_level(_contexts,	"day-fibbonachi", "60"),	na, na, i_day_fibb_color_5, false)
	level_render_zone("day-fibb-70",	contexts_get_level(_contexts,	"day-fibbonachi", "70"),	na, na, i_day_fibb_color_6, false)
	level_render_zone("day-fibb-100",	contexts_get_level(_contexts,	"day-fibbonachi", "100"),	na, na, i_day_fibb_color_7, false)

	//  ====================================================================================================================================================
	//      Render EMA
	//  ====================================================================================================================================================

plotshape(i_ema_markers	and	contexts_get_bool(_contexts, "ema", "1-6.signal.enter"),	"EMA Uptrend - Enter",		style = shape.circle,		size = _shape_size,  location = location.belowbar,	color = i_ema_green,    display = display.pane)
plotshape(i_ema_markers	and	contexts_get_bool(_contexts, "ema", "1-6.signal.exit"),		"EMA Uptrend - Exit",		style = shape.circle,		size = _shape_size,  location = location.belowbar,	color = i_ema_red,		display = display.pane)

plotshape(i_ema_markers	and	contexts_get_bool(_contexts, "ema", "6-1.signal.enter"),	"EMA Downtrend - Enter",	style = shape.circle,		size = _shape_size,  location = location.abovebar,	color = i_ema_green,	display = display.pane)
plotshape(i_ema_markers	and	contexts_get_bool(_contexts, "ema", "6-1.signal.exit"),		"EMA Downtrend - Exit",		style = shape.circle,		size = _shape_size,  location = location.abovebar,	color = i_ema_red,		display = display.pane)

	//  ====================================================================================================================================================
	//      CCI markers
	//  ====================================================================================================================================================

plotshape(i_cci_markers and contexts_get_bool(_contexts, "cci", "below.exit"),		"CCI Below - Exit",		    style = shape.labelup,		size = _shape_size,  location = location.belowbar,	color = color.new(color.teal, _label_alpha),         display = display.pane)
plotshape(i_cci_markers and contexts_get_bool(_contexts, "cci", "below.exit"),		"CCI Below - Exit",		    style = shape.diamond,		size = _shape_size,  location = location.belowbar,	color = color.new(color.teal, _label_alpha),         display = display.pane)

plotshape(i_cci_markers and contexts_get_bool(_contexts, "cci", "above.exit"),		"CCI Above - Exit",		    style = shape.labeldown,	size = _shape_size,  location = location.abovebar,	color = color.new(color.fuchsia, _label_alpha),		display = display.pane)
plotshape(i_cci_markers and contexts_get_bool(_contexts, "cci", "above.exit"),		"CCI Above - Exit",		    style = shape.diamond,		size = _shape_size,  location = location.abovebar,	color = color.new(color.fuchsia, _label_alpha),		display = display.pane)

	//	---

plotshape(i_cci_markers and contexts_get_bool(_contexts, "cci", "lower.signal"),	"CCI Zero - Lower",			style = shape.labeldown,	size = _shape_size,  location = location.abovebar,	color = color.orange,		display = display.pane)
plotshape(i_cci_markers and contexts_get_bool(_contexts, "cci", "higher.signal"),	"CCI Zero - Higher",		style = shape.labelup,		size = _shape_size,  location = location.belowbar,	color = color.blue,		display = display.pane)

	//	---

plotshape(i_cci_markers and contexts_get_bool(_contexts, "cci", "below.enter"),		"CCI Below - Enter",		style = shape.labeldown,	size = _shape_size,  location = location.belowbar,	color = color.fuchsia,     display = display.pane)
plotshape(i_cci_markers and contexts_get_bool(_contexts, "cci", "below.enter"),		"CCI Below - Enter",		style = shape.xcross,		size = _shape_size,  location = location.belowbar,	color = color.fuchsia,		display = display.pane)

plotshape(i_cci_markers and contexts_get_bool(_contexts, "cci", "above.enter"),		"CCI Above - Enter",		style = shape.labelup,		size = _shape_size,  location = location.abovebar,	color = color.teal,      	display = display.pane)
plotshape(i_cci_markers and contexts_get_bool(_contexts, "cci", "above.enter"),		"CCI Above - Enter",		style = shape.xcross,		size = _shape_size,  location = location.abovebar,	color = color.teal,      	display = display.pane)
